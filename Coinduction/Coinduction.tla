------------------------------ MODULE Coinduction ------------------------------

(**************************************************)
(* X finite set                                   *)
(* (SUBSET X, \subseteq) forms a complete lattice *)
(**************************************************)

EXTENDS Naturals, TLC

\* power set
P(X) == SUBSET X

\* monotone functions over X
Monotonic(X) == {
    F \in [ P(X) -> P(X) ] :
        \A S, T \in P(X) :
            S \subseteq T => F[S] \subseteq F[T]
}

\* inductive set of F
Inductive(F, X) == X \ UNION { X \ SS : SS \in { S \in P(X) : F[S] \subseteq S } }

\* coinductive set of F
Coinductive(F, X) == UNION { S \in P(X) : S \subseteq F[S] }

\* monotone functions over {0, 1}
G0 == [ S \in P({0, 1}) |-> {0} ]
G1 == [ S \in P({0, 1}) |-> {1} ]
G2 == [ S \in P({0, 1}) |-> IF S = {} THEN {} ELSE {0} ]
G3 == [ S \in P({0, 1}) |->
    CASE S = {0} -> {1}
      [] S = {1} -> {0} 
      [] OTHER -> S
]

Tests ==
    \*  inductive + coinductive set examples
    /\  Assert(  Inductive(G0, {0, 1}) = {0}   , "inductive G0"  )
    /\  Assert(Coinductive(G0, {0, 1}) = {0}   , "coinductive G0")
    /\  Assert(  Inductive(G1, {0, 1}) = {1}   , "inductive G1"  )
    /\  Assert(Coinductive(G1, {0, 1}) = {1}   , "coinductive G1")
    /\  Assert(  Inductive(G2, {0, 1}) = { }   , "inductive G2"  )
    /\  Assert(Coinductive(G2, {0, 1}) = {0}   , "coinductive G2")
    /\  Assert(  Inductive(G3, {0, 1}) = { }   , "inductive G3"  )
    /\  Assert(Coinductive(G3, {0, 1}) = {0, 1}, "coinductive G3")
    \*  all monotonic functions have larger coinductive sets
    /\  Assert(
            \A F \in Monotonic({0, 1}) :
                Inductive(F, {0, 1}) \subseteq Coinductive(F, {0, 1}),
            "inductive-coinductive monotonic functions"
        )
    \*  explicit set of monotonic functions with fixed points
    /\  Assert(
            \A F \in Monotonic({0, 1}) :
                Inductive(F, {0, 1}) = Coinductive(F, {0, 1})
                    <=>
                    F \in
                    { ({} :> {} @@ {0} :> {} @@ {1} :> {} @@ {0, 1} :> {})
                    , ({} :> {} @@ {0} :> {} @@ {1} :> {} @@ {0, 1} :> {0})
                    , ({} :> {} @@ {0} :> {} @@ {1} :> {} @@ {0, 1} :> {1})
                    , ({} :> {} @@ {0} :> {} @@ {1} :> {0} @@ {0, 1} :> {0})
                    , ({} :> {} @@ {0} :> {1} @@ {1} :> {} @@ {0, 1} :> {1})
                    , ({} :> {0} @@ {0} :> {0} @@ {1} :> {0} @@ {0, 1} :> {0})
                    , ({} :> {0} @@ {0} :> {0, 1} @@ {1} :> {0} @@ {0, 1} :> {0, 1})
                    , ({} :> {0} @@ {0} :> {0, 1} @@ {1} :> {0, 1} @@ {0, 1} :> {0, 1})
                    , ({} :> {1} @@ {0} :> {1} @@ {1} :> {1} @@ {0, 1} :> {1})
                    , ({} :> {1} @@ {0} :> {1} @@ {1} :> {0, 1} @@ {0, 1} :> {0, 1})
                    , ({} :> {1} @@ {0} :> {0, 1} @@ {1} :> {0, 1} @@ {0, 1} :> {0, 1})
                    , ({} :> {0, 1} @@ {0} :> {0, 1} @@ {1} :> {0, 1} @@ {0, 1} :> {0, 1})
                    },
            "fixed points"
        )
    \*  explicit set of monotonic functions over {0, 1}
    /\  Assert(Monotonic({0, 1}) =
            { ({} :> {} @@ {0} :> {} @@ {1} :> {} @@ {0, 1} :> {})
            , ({} :> {} @@ {0} :> {} @@ {1} :> {} @@ {0, 1} :> {0})
            , ({} :> {} @@ {0} :> {} @@ {1} :> {} @@ {0, 1} :> {1})
            , ({} :> {} @@ {0} :> {} @@ {1} :> {} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {} @@ {1} :> {0} @@ {0, 1} :> {0})
            , ({} :> {} @@ {0} :> {} @@ {1} :> {0} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {} @@ {1} :> {1} @@ {0, 1} :> {1})
            , ({} :> {} @@ {0} :> {} @@ {1} :> {1} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {} @@ {1} :> {0, 1} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {0} @@ {1} :> {} @@ {0, 1} :> {0})
            , ({} :> {} @@ {0} :> {0} @@ {1} :> {} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {0} @@ {1} :> {0} @@ {0, 1} :> {0})
            , ({} :> {} @@ {0} :> {0} @@ {1} :> {0} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {0} @@ {1} :> {1} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {0} @@ {1} :> {0, 1} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {1} @@ {1} :> {} @@ {0, 1} :> {1})
            , ({} :> {} @@ {0} :> {1} @@ {1} :> {} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {1} @@ {1} :> {0} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {1} @@ {1} :> {1} @@ {0, 1} :> {1})
            , ({} :> {} @@ {0} :> {1} @@ {1} :> {1} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {1} @@ {1} :> {0, 1} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {0, 1} @@ {1} :> {} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {0, 1} @@ {1} :> {0} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {0, 1} @@ {1} :> {1} @@ {0, 1} :> {0, 1})
            , ({} :> {} @@ {0} :> {0, 1} @@ {1} :> {0, 1} @@ {0, 1} :> {0, 1})
            , ({} :> {0} @@ {0} :> {0} @@ {1} :> {0} @@ {0, 1} :> {0})
            , ({} :> {0} @@ {0} :> {0} @@ {1} :> {0} @@ {0, 1} :> {0, 1})
            , ({} :> {0} @@ {0} :> {0} @@ {1} :> {0, 1} @@ {0, 1} :> {0, 1})
            , ({} :> {0} @@ {0} :> {0, 1} @@ {1} :> {0} @@ {0, 1} :> {0, 1})
            , ({} :> {0} @@ {0} :> {0, 1} @@ {1} :> {0, 1} @@ {0, 1} :> {0, 1})
            , ({} :> {1} @@ {0} :> {1} @@ {1} :> {1} @@ {0, 1} :> {1})
            , ({} :> {1} @@ {0} :> {1} @@ {1} :> {1} @@ {0, 1} :> {0, 1})
            , ({} :> {1} @@ {0} :> {1} @@ {1} :> {0, 1} @@ {0, 1} :> {0, 1})
            , ({} :> {1} @@ {0} :> {0, 1} @@ {1} :> {1} @@ {0, 1} :> {0, 1})
            , ({} :> {1} @@ {0} :> {0, 1} @@ {1} :> {0, 1} @@ {0, 1} :> {0, 1})
            , ({} :> {0, 1} @@ {0} :> {0, 1} @@ {1} :> {0, 1} @@ {0, 1} :> {0, 1})
            },
            "monotonic functions over {0, 1}"
        )

================================================================================
